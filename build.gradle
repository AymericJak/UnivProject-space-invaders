/*
 * La configuration de Gradle pour compiler et exécuter le projet.
 */

// Les plugins permettant à Gradle de gérer le projet en tant qu'application JavaFX.
plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.beryx.jlink' version '2.26.0'
    id 'eclipse'
}

eclipse.classpath.file.whenMerged {
    entries.findAll { it.properties.kind.equals('lib') }.each {
        it.entryAttributes['module'] = 'true'
    }
}


// Les dépôts d'où les dépendances doivent être téléchargées.
repositories {
    mavenCentral()
}

// La version du langage Java utilisée dans le projet.
sourceCompatibility = '17'
targetCompatibility = '17'

// Le nom de la classe contenant la méthode "main".
application {
    mainModule = 'fr.univartois.butinfo.qdev2.spaceinvaders'
    mainClass = 'fr.univartois.butinfo.qdev2.spaceinvaders.SpaceInvaders'
}

// La version de JavaFX utilisée dans le projet, et les composants requis.
javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'space-invaders'
    }
}
